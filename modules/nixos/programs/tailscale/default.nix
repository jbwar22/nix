{ config, lib, pkgs, ... }:

with lib; with ns config ./.; (let
  users = config.custom.common.opts.host.users;
  admins = getAdmins users;
  tsop = getTSOp users;
in {
  options = opt {
    enable = mkEnableOption "tailscale";
    serviceContainer = {
      enable = mkEnableOption "services";
      volumesRoot = mkOption {
        type = with types; nullOr str;
        description = "dir where volumes live";
        default = null;
      };
      tag = mkOption {
        type = types.str;
        default = "tag:container";
      };
    };
  };

  config = mkMerge [
    {
      services.tailscale = let
        flags = [ "--accept-routes=true" "--operator=${tsop}" ];
      in {
        enable = true;
        useRoutingFeatures = "client";
        package = pkgs.tailscale;
        extraUpFlags = flags;
        extraSetFlags = flags;
      };

      services.davfs2 = {
        enable = true;
      };

      users = setUserGroups admins [ "davfs2" ];

      custom.nixos.behavior.impermanence.paths = [ "/var/lib/tailscale" ];
    }
    (mkIf cfg.serviceContainer.enable {
      virtualisation.oci-containers.containers."monstro-services-tailscale" = {
        image = "tailscale/tailscale:latest";
        environment = {
          "TS_EXTRA_ARGS" = "--advertise-tags=${cfg.serviceContainer.tag}";
          "TS_SERVE_CONFIG" = "/config/serve.json";
          "TS_STATE_DIR" = "/var/lib/tailscale";
          "TS_USERSPACE" = "true";
        };
        environmentFiles = [ (ageOrBust config "tailscale-services-env") ];
        volumes = [
          "${cfg.serviceContainer.volumesRoot}/tailscale/config:/config:rw"
          "${cfg.serviceContainer.volumesRoot}/tailscale/start.sh:/start.sh:rw" # TODO age secret this!
          "${cfg.serviceContainer.volumesRoot}/tailscale/state:/var/lib/tailscale:rw"
        ];
        cmd = [ "/start.sh" ];
        log-driver = "journald";
        extraOptions = [
          "--cap-add=net_admin"
          "--device=/dev/net/tun:/dev/net/tun:rwm"
          "--hostname=monstro-services"
          "--network=host"
          "--pull=always"
        ];
      };

      systemd.services."docker-monstro-services-tailscale" = {
        serviceConfig = {
          Restart = lib.mkOverride 90 "always";
          RestartMaxDelaySec = lib.mkOverride 90 "1m";
          RestartSec = lib.mkOverride 90 "100ms";
          RestartSteps = lib.mkOverride 90 9;
        };
        partOf = [
          "docker-compose-monstro-services-tailscale-root.target"
        ];
        wantedBy = [
          "docker-compose-monstro-services-tailscale-root.target"
        ];
      };

      systemd.targets."docker-compose-monstro-services-tailscale-root" = {
        unitConfig = {
          Description = "Root target generated by compose2nix.";
        };
        wantedBy = [ "multi-user.target" ];
      };
    })
  ];
})
